# Jedi Knight Level Cog Script, der, for a level
#
# HellSkyChange.cog
#
# The sky in Hell never really changed, but Crack Toking Sketch
# Decided to make the sky in Hell change, so like everything sketch
# has done, Shred has done it 100 times better. ;D
#
# Created by: Hell Raiser [Temorarily Under Identity Crisis]

flags=0x240

symbols

#Where to start it at, 1=dayish or 0=nightish
int			Start=0

#The sky materials
material	Sky0
material	Sky1
material	Sky2
material	Sky3
material	Sky4

#The total time it takes to fade in seconds
flex		FadeTime=3.3

#The time between fades in seconds
flex		SwitchTime=300

int			Mode		local
int			CurrentCel	local

message		startup
message		timer

end

code

startup:

	Mode=Start;
	SetTimerEx(SwitchTime, 1, -1, -1);
	if(Start==1)
	{
		for(x=0;x<5;x=x+1)
		{SetMaterialCel(Sky0[x], 0);}
		CurrentCel=0;
	}
	else
	{
		for(x=0;x<5;x=x+1)
		{SetMaterialCel(Sky0[x], 10);}
		CurrentCel=10;
	}

return;

timer:

	if(GetSenderID() == 1)
	{
		SetTimerEx(FadeTime/11, 2, -1, -1);
	}
	else if(GetSenderID() == 2)
	{
		if(Mode==1)
		{
			CurrentCel=CurrentCel+1;
			for(x=0;x<5;x=x+1)
			{SetMaterialCel(Sky0[x], CurrentCel);}
			if(CurrentCel != 10)
			{SetTimerEx(FadeTime/11, 2, -1, -1);}
			else
			{
				Mode=0;
				SetTimerEx(SwitchTime, 1, -1, -1);
			}
		}
		else
		{
			CurrentCel=CurrentCel-1;
			for(x=0;x<5;x=x+1)
			{SetMaterialCel(Sky0[x], CurrentCel);}
			if(CurrentCel != 0)
			{SetTimerEx(FadeTime/11, 2, -1, -1);}
			else
			{
				Mode=1;
				SetTimerEx(SwitchTime, 1, -1, -1);
			}
		}
	}

return;

end